/***********************************************************************************/
/*    program name:     From ESP8266 to Real time database 
/*    programer name:   Stefan Zakutansky
/*    date:             2nd February 2024
/*    Upload to:        ESP8266
/*    program function: Receive data from ATmega328 with start and end-markers
/*                      combined. Once separated, manipulate and wrap all together
/*                      for sending it to data base via WIFI
/*    Credits to:       Rui Santos, Robin2, JANAK13
/*
/************************************************************************************/

#include <Wire.h>
#include <Arduino.h>
#include <WiFiUdp.h>
#include <NTPClient.h>
#include <ESP8266WiFi.h>
#include <SoftwareSerial.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BME280.h>
#include <Firebase_ESP_Client.h>

// Provide the token generation process info.
#include "addons/TokenHelper.h"
// Provide the RTDB payload printing info and other helper functions.
#include "addons/RTDBHelper.h"

// Insert your network credentials
#define WIFI_SSID "VODAFONE-1221"
#define WIFI_PASSWORD "fdRb9shdKKCTC2dq"

// Insert Firebase project API Key
#define API_KEY "AIzaSyCYRPVrqhwbLWHLtGmyUmLsXv_62e8FAK8"

// Insert Authorized Email and Corresponding Password
#define USER_EMAIL "stefan.zakutansky@gmail.com"
#define USER_PASSWORD "esp32.!"

// Insert RTDB URLefine the RTDB URL
#define DATABASE_URL "https://my-solar-station-default-rtdb.europe-west1.firebasedatabase.app/"



const byte numChars = 32;
char receivedChars[numChars];
char tempChars[numChars];        // temporary array for use when parsing

      // variables to hold the parsed data
char messageFromPC[numChars] = {0};
int integerFromPC = 0;
float floatFromPC = 0.0;
#define RX 5
#define TX 4

//Define software serial object in the class
SoftwareSerial ArduinoUno(RX,TX);   //D1=5=rx  D2=4=Tx
// Define Firebase objects
FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;
// Parent Node (to be updated in every loop)
String parentPath;
FirebaseJson json;
// Define NTP Client to get time
WiFiUDP ntpUDP;
NTPClient timeClient(ntpUDP, "pool.ntp.org");
// Variable to save USER UID
String uid;
// Database main path (to be updated in setup with the user UID)
String databasePath;

// Database child nodes
String messageFromPCPath = "/temperature";
String integerFromPCPath = "/humidity";
String floatFromPCPath = "/pressure";
String timePath = "/timestamp";
// Variable to save current epoch time
int timestamp;
boolean recvInProgress = false;
boolean newData = false;
byte ndx = 0;
static char startMarker = '<';
static char endMarker = '>';
char rc;
// Timer variables (send new readings every three minutes)
unsigned long sendDataPrevMillis = 0;
unsigned long timerDelay = 18000;
int NrOfCycles=0;
int connectionOK = 0;


// Initialize WiFi
void initWiFi() {
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  Serial.print("Connecting to WiFi ..");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print('.');
    delay(1000);
  }
  Serial.println(WiFi.localIP());
  Serial.println();
}

// Function that gets current epoch time
unsigned long getTime() {
  timeClient.update();
  unsigned long now = timeClient.getEpochTime();
  return now;
}

void setup(){
  //init. hardware serial callout
  Serial.begin(115200);

  //init. software serial callout
  ArduinoUno.begin(9600);

  //init. wifi callout
  initWiFi();

  //init. time gathering callout
  timeClient.begin();

  // Assign the api key (required)
  config.api_key = API_KEY;

  // Assign the user sign in credentials
  auth.user.email = USER_EMAIL;
  auth.user.password = USER_PASSWORD;

  // Assign the RTDB URL (required)
  config.database_url = DATABASE_URL;

  //check if connected to sewrver and set the baud rate
  Firebase.reconnectWiFi(true);
  fbdo.setResponseSize(4096);

  // Assign the callback function for the long running token generation task */
  config.token_status_callback = tokenStatusCallback; //see addons/TokenHelper.h

  // Assign the maximum retry of token generation
  config.max_token_generation_retry = 5;

  // Initialize the library with the Firebase authen and config
  Firebase.begin(&config, &auth);

  // Getting the user UID might take a few seconds
  Serial.println("Getting User UID");
  while ((auth.token.uid) == "") {
    Serial.print('.');
    delay(1000);
  }

  // Print user UID
  uid = auth.token.uid.c_str();
  Serial.print("User UID: ");
  Serial.println(uid);

  // Update database path
  databasePath = "/UsersData/" + uid + "/readings";
}



void loop(){
  //check if data are comming through and if previous data already sent
  if (ArduinoUno.available()>0 && newData == false){
    //store incoming
    rc = ArduinoUno.read();
    //figgure out the start of the incoming strinhg 
    if (recvInProgress == true) {
        if (rc != endMarker) {
            receivedChars[ndx] = rc;
            ndx++;
            if (ndx >= numChars) {
                ndx = numChars - 1;
            }
        }
        else {
            receivedChars[ndx] = '\0'; // terminate the string
            recvInProgress = false;
            ndx = 0;
            newData = true;
        }
      }
      else if (rc == startMarker) {
          recvInProgress = true;
      }  
    }

  if (newData == true ){
    //if new data in the buffer, break the down in sepoarate blocks and assigne the type  
    strcpy(tempChars,receivedChars);
    // this temporary copy is necessary to protect the original data
    // because strtok() used in parseData() replaces the commas with \0
    char * strtokIndx; // this is used by strtok() as an index

    strtokIndx = strtok(tempChars,",");    // get the first part - the string
    strcpy(messageFromPC, strtokIndx);     // copy it to messageFromPC

    strtokIndx = strtok(NULL, ",");        // this continues where the previous call left off
    integerFromPC = atoi(strtokIndx);      // convert this part to an integer

    strtokIndx = strtok(NULL, ",");
    floatFromPC = atof(strtokIndx);        // convert this part to a float

    // transmission of data every 16 seconds for Appinventor to be ready to receive data
    if (Firebase.ready() && (millis() - sendDataPrevMillis > timerDelay || sendDataPrevMillis == 0)){
      sendDataPrevMillis = millis();
      //output incoming data to hardware serial / verification of data is actualy processed
      Serial.println(messageFromPC);
      Serial.print("Integer ");
      Serial.println(integerFromPC);
      Serial.print("Float ");
      Serial.println(floatFromPC);
      //Get current timestamp
      timestamp = getTime();
      Serial.print ("time: ");
      Serial.println (timestamp);
      //build the string for dispatch to server
      parentPath = databasePath + "/" + String(timestamp);
      //assigne data to json
      json.set(messageFromPCPath.c_str(), String(messageFromPC));
      json.set(integerFromPCPath.c_str(), String(integerFromPC));  
      json.set(floatFromPCPath.c_str(), String(floatFromPC));
      json.set(timePath, String(timestamp));
      Serial.printf("Set json... %s\n", Firebase.RTDB.setJSON(&fbdo, parentPath.c_str(), &json) ? "ok" : fbdo.errorReason().c_str());
      //data has been set flag     
      newData = false;
    }
  }
}

